buildscript {
    ext.kotlin_version = '1.8.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.15'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

// Define namespace mappings for plugins that don't specify one
def pluginNamespaces = [
    'flutter_nearby_connections': 'com.nankai.flutter_nearby_connections',
    'nearby_service': 'np.com.susanthapa.nearby_service',
    'qr_code_scanner': 'net.touchcapture.qr.flutterqr',
    'flutter_webrtc': 'com.cloudwebrtc.webrtc',
    'workmanager': 'be.tramckrijte.workmanager'
]

subprojects { subproject ->
    afterEvaluate {
        if (subproject.plugins.hasPlugin('com.android.library')) {
            // Set namespace for plugins that don't have one defined
            android {
                if (namespace == null) {
                    def pluginNamespace = pluginNamespaces[subproject.name]
                    if (pluginNamespace != null) {
                        namespace pluginNamespace
                    }
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
